;奉仕精神のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化

;-------------------------------------------------
;奉仕精神のLvUP
;-------------------------------------------------
@ABLUP16
DRAWLINE
;PRINTL 奴隷の奉仕精神を高めます
;PRINTL 奉仕精神が高いと、性的な奉仕に歓びを感じるようになります
;DRAWLINE
;奉仕精神はLv5が上限
;[愛][妄信][献身的]が付いている場合はLv10まで解放
;[セフレ]で7まで解放。
IF ABL:奉仕精神 >= 5 && (TALENT:献身的 == 0 && TALENT:恋心 == 0 && TALENT:妄信 == 0 && TALENT:セフレ == 0)
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:奉仕精神 >= 7 && (TALENT:献身的 == 0 && TALENT:恋心 == 0 && TALENT:妄信 == 0)
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:奉仕精神 >= 10
	PRINTW 既にMAXです
	RETURN 0
ENDIF

;必要な屈服の珠
A = 0
;必要な恭順の珠
B = 0
;必要な習得の珠
C = 0
;必要な奉仕快楽経験回数
D = 0
;必要な絶頂経験＆精液経験回数
E = 0
;必要な異常経験回数
F = 0

;条件別にＯＫかダメかを記録する
;屈服の珠による可否（I=0:可、I&1:珠不足、I&2:経験不足、I&4:能力不足）
I = 0
;恭順の珠による可否（J=0:可、J&1:珠不足、J&2:経験不足、J&4:能力不足、J=256:そのレベルでは自動不可）
J = 0
;習得の珠による可否（K=0:可、K&1:珠不足、K&2:経験不足、K&4:能力不足、K=256:そのレベルでは自動不可）
K = 0

CALL CAN_ABLUP16

;従順が奉仕精神＋１レベルでないといけない
PRINTFORML %ABLNAME:[[ABL:信頼]]%{ABL:奉仕精神+1}LV以上(現在{ABL:信頼}LV)かつ

;ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[妄信]なら無視できる）
SIF F > 0
	PRINTFORML %EXPNAME:[[EXP:異常経験]]%{F}以上(現在{EXP:異常経験})かつ

;屈服の珠で上げる
PRINTFORM [0] - %PALAMNAME:[[PALAM:屈服]]%の珠×{JUEL:屈服}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 
IF E > 0
	PRINTFORML 　　　%EXPNAME:[[EXP:絶頂経験]]%　{EXP:絶頂経験}/{E}
	PRINTFORML 　　　%EXPNAME:[[EXP:精液経験]]%　{EXP:精液経験}/{E}
ENDIF

IF B > 0
	;恭順の珠で上げる
	PRINTFORM [1] - %PALAMNAME:[[PALAM:恭順]]%の珠×{JUEL:恭順}/{B} ……
	IF J == 0
		PRINT ＯＫ
	ELSE
		SIF J & 1
			PRINT 珠不足 
		SIF J & 2
			PRINT 経験不足 
		SIF J & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
	SIF D > 0
		PRINTFORML 　　　%EXPNAME:[[EXP:奉仕快楽経験]]%　{EXP:奉仕快楽経験}/{D}
ENDIF

IF C > 0
	;習得の珠で上げる
	PRINTFORM [2] - %PALAMNAME:[[PALAM:習得]]%の珠×{JUEL:習得}/{C} ……
	IF K == 0
		PRINT ＯＫ
	ELSE
		SIF K & 1
			PRINT 珠不足 
		SIF K & 2
			PRINT 経験不足 
		SIF K & 4
			PRINT 能力不足 
	ENDIF
	PRINTL 
	PRINTFORML 　　　%EXPNAME:[[EXP:絶頂経験]]%　{EXP:絶頂経験}/1
ENDIF

PRINTL [100] - やめる

$INPUT_LOOP
INPUT
IF (RESULT < 0 || RESULT > 2) && RESULT != 100
	GOTO INPUT_LOOP
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	GOTO INPUT_LOOP
ELSEIF J == 256 && RESULT == 1
	GOTO INPUT_LOOP
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	GOTO INPUT_LOOP
ELSEIF K == 256 && RESULT == 2
	GOTO INPUT_LOOP
ELSEIF K != 0 && RESULT == 2
	PRINTL 条件を満たしていません
	GOTO INPUT_LOOP
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:奉仕精神 += 1

IF RESULT == 0
	JUEL:屈服 -= A
ELSEIF RESULT == 1
	JUEL:恭順 -= B
ELSEIF RESULT == 2
	JUEL:習得 -= C
ENDIF

PRINTFORML %ABLNAME:[[ABL:奉仕精神]]%のレベルが{ABL:奉仕精神}になりました

RETURN 0

;-------------------------------------------------
;奉仕精神のLvUP可否判定
;-------------------------------------------------
@CAN_ABLUP16
SIF ABL:奉仕精神 >= 10
	RETURN 0
SIF ABL:奉仕精神 >= 5 && (TALENT:献身的 == 0 && TALENT:恋心 == 0 && TALENT:妄信 == 0 && TALENT:セフレ == 0)
	RETURN 0
SIF ABL:奉仕精神 >= 7 && (TALENT:献身的 == 0 && TALENT:恋心 == 0 && TALENT:妄信 == 0)
	RETURN 0

;判定変数を空に
I = 0
J = 0
K = 0

A = 0
B = 0
C = 0
D = 0
E = 0
F = 0



IF ABL:奉仕精神 == 0
	A = 100
	B = 20
	C = 100
	D = 1
	E = 1
ELSEIF ABL:奉仕精神 == 1
	A = 1200
	B = 400
	C = 2000
	D = 1
	E = 3
ELSEIF ABL:奉仕精神 == 2
	A = 5000
	B = 2000
	C = 10000
	D = 20
	E = 6
ELSEIF ABL:奉仕精神 == 3
	A = 10000
	B = 3000
	C = 0
	D = 20
	E = 10
ELSEIF ABL:奉仕精神 == 4
	A = 30000
	B = 10000
	C = 0
	D = 100
	E = 20
ELSEIF ABL:奉仕精神 == 5
	A = 50000
	B = 20000
	C = 0
	D = 200
	E = 80
ELSEIF ABL:奉仕精神 == 6
	A = 70000
	B = 30000
	C = 0
	D = 350
	E = 150
ELSEIF ABL:奉仕精神 == 7
	A = 100000
	B = 50000
	C = 0
	D = 500
	E = 200
ELSEIF ABL:奉仕精神 == 8
	A = 150000
	B = 80000
	C = 0
	D = 700
	E = 400
ELSEIF ABL:奉仕精神 == 9
	A = 200000
	B = 150000
	C = 0
	D = 1000
	E = 800
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:奉仕精神 == 3
		TIMES A , 1.50
		TIMES B , 1.50
		TIMES D , 1.50
		TIMES E , 1.50
	ELSEIF ABL:奉仕精神 == 4
		TIMES A , 2.00
		TIMES B , 2.00
		TIMES D , 2.00
		TIMES E , 2.00
	ELSEIF ABL:奉仕精神 == 5
		TIMES A , 2.50
		TIMES B , 2.50
		TIMES D , 2.50
		TIMES E , 2.50
	ELSEIF ABL:奉仕精神 >= 6
		TIMES A , 3.00
		TIMES B , 3.00
		TIMES D , 3.00
		TIMES E , 3.00
	ENDIF
ENDIF

;ＬＶ３以上に上げるときは異常経験必要（素質：[献身的][愛][妄信]なら無視できる）
IF ABL:奉仕精神 >= 3 && (TALENT:献身的 == 0 && TALENT:恋心 == 0 && TALENT:妄信 == 0)
	F = ABL:奉仕精神 - 2
ENDIF

;反抗的
IF TALENT:ナマイキ
	TIMES A , 1.20
	TIMES B , 1.40
	TIMES D , 1.20
	TIMES E , 1.20
ENDIF

;気丈
IF TALENT:気丈
	TIMES A , 1.20
	TIMES D , 1.20
ENDIF

;素直
IF TALENT:素直
	TIMES A , 0.90
	TIMES B , 0.80
	TIMES C , 0.80
	TIMES D , 0.70
ENDIF

;生意気
IF TALENT:活発
	TIMES A , 1.10
	TIMES B , 1.10
ENDIF

;プライド高い
IF TALENT:プライド高い
	TIMES A , 1.40
	TIMES B , 1.30
	TIMES D , 1.20
	TIMES E , 1.20
;プライド低い
ELSEIF TALENT:プライド低い
	TIMES A , 0.90
	TIMES B , 0.80
	TIMES D , 0.80
	TIMES E , 0.80
ENDIF

;永遠の愛
IF TALENT:永遠の愛
	TIMES A , 0.20
	TIMES B , 0.20
	TIMES D , 0.20
	TIMES E , 0.20
ENDIF
;女神
IF TALENT:天使
	TIMES B , 0.10
	TIMES D , 0.10
ENDIF
;自制心
IF TALENT:自制心
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES D , 1.20
	TIMES E , 1.20
ENDIF

;無関心
IF TALENT:クール
	TIMES A , 1.10
	TIMES B , 1.10
	TIMES D , 1.20
	TIMES E , 1.10
ENDIF

;感情乏しい
IF TALENT:感情乏しい
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES D , 1.40
	TIMES E , 1.20
ENDIF

;保守的
IF TALENT:保守的
	TIMES A , 1.30
	TIMES B , 1.30
	TIMES C , 1.30
	TIMES D , 1.20
	TIMES E , 1.20
ENDIF

;楽観的
IF TALENT:ポジティブ
	TIMES A , 0.90
	TIMES B , 0.80
	TIMES D , 0.70
	TIMES E , 0.75
;悲観的
ELSEIF TALENT:ネガティブ
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES D , 0.80
	TIMES E , 0.80
ENDIF

;目立ちたがり
IF TALENT:目立ちたがり
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
	TIMES D , 0.90
ENDIF

;抑圧
IF TALENT:抑圧
	TIMES A , 1.10
	TIMES B , 1.10
	TIMES D , 2.50
	TIMES E , 3.00
;解放
ELSEIF TALENT:解放
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES D , 0.70
	TIMES E , 0.60
ENDIF

;抵抗
IF TALENT:抵抗
	TIMES A , 1.50
	TIMES B , 1.50
	TIMES C , 1.50
	TIMES D , 2.00
	TIMES E , 2.00
ENDIF

;弱味
IF TALENT:弱味
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
	TIMES D , 0.50
	TIMES E , 0.50
ENDIF

;習得早い
SIF TALENT:素質
	TIMES C , 0.80

;習得遅い
SIF TALENT:習得遅い
	TIMES C , 1.60

;舌使い
SIF TALENT:舌使い
	TIMES C , 0.80

;献身的
IF TALENT:献身的
	TIMES A , 0.80
	TIMES B , 0.70
	TIMES D , 0.60
	TIMES E , 0.80
ENDIF

;快感に素直
IF TALENT:快感に素直
	TIMES E , 0.70
;快感の否定
ELSEIF TALENT:快感の否定
	TIMES E , 3.00
ENDIF

;即落ち
IF TALENT:即落ち
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES D , 0.50
	TIMES E , 0.50
ENDIF

;倒錯的
IF TALENT:倒錯的
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
	TIMES D , 0.75
	TIMES E , 0.75
ENDIF

;サド
IF TALENT:サド
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES D , 1.50
ENDIF

;愛
IF TALENT:恋心
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
	TIMES D , 0.75
ENDIF
;妹
IF TALENT:妹
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
	TIMES D , 0.80
ENDIF
;幼馴染み
IF TALENT:幼馴染み
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
	TIMES D , 0.80
ENDIF
;姉
IF TALENT:姉
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
	TIMES D , 0.80
ENDIF
;大和撫子
IF TALENT:大和撫子
	TIMES A , 0.60
	TIMES B , 0.60
	TIMES C , 0.60
	TIMES D , 0.60
ENDIF
;妄信
IF TALENT:妄信
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
	TIMES D , 0.50
	TIMES E , 0.50
ENDIF

;小悪魔
IF TALENT:小悪魔
	TIMES A , 1.10
	TIMES B , 1.10
	TIMES D , 1.20
	TIMES E , 0.80
ENDIF

;狂気
IF TALENT:狂気
	TIMES A , 2.50
	TIMES B , 3.00
	TIMES C , 1.50
	TIMES D , 0.80
	TIMES E , 0.50
ENDIF

;崩壊
IF TALENT:崩壊
	TIMES A , 2.50
	TIMES B , 2.50
	TIMES C , 2.50
	TIMES D , 0.50
	TIMES E , 0.50
ENDIF

;マネージャー
IF TALENT:マネージャー
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES D , 0.80
	TIMES E , 0.80
ENDIF

;最低でも1回・1個は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1
SIF D < 1
	D = 1
SIF E < 1
	E = 1

;屈服の珠は足りている？
SIF JUEL:屈服 < A
	I |= 1

;恭順の珠は足りている？
IF B > 0
	SIF JUEL:恭順 < B
		J |= 1
	;この場合奉仕快楽経験が必要なことあり
	SIF EXP:奉仕快楽経験 < D
		J |= 2
ELSE
	J = 256
ENDIF

;習得の珠は足りている？
IF C > 0
	SIF JUEL:習得 < C
		K |= 1
	;この場合絶頂経験が１以上必要
	SIF EXP:絶頂経験 < 1
		K |= 2
ELSE
	K = 256
ENDIF

;絶頂経験が必要な場合
SIF EXP:絶頂経験 < E
	I |= 2
;精液経験が必要な場合
SIF EXP:精液経験 < E
	I |= 2

;従順が奉仕精神＋１レベルでないと能力不足
IF ABL:信頼 < ABL:奉仕精神 + 1
	I |= 4
	J |= 4
	K |= 4
ENDIF

;異常経験が不足
IF F > EXP:異常経験
	I |= 2
	J |= 2
	K |= 2
ENDIF

IF I == 0 || J == 0 || K == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;