;精液中毒のLvUP処理とその可否判定
;eratohoA ver1,204のスクリプトをベースに処理を簡略化

;-------------------------------------------------
;精液中毒のLvUP
;-------------------------------------------------
@ABLUP32
DRAWLINE
;PRINTL 奴隷の精液への執着心を高めます
;PRINTL 精液中毒が高くなると、精液を浴びたり飲んだりする事への満足感が深まり、
;PRINTL 常に精液の匂いをかいでいないと心が落ち着かなくなります
;DRAWLINE
;精液中毒はLv5が上限
;[淫乱][習得早い][汚臭鈍感][汚れ無視][精愛味覚]が付いている場合はLv10まで解放
IF ABL:精液中毒 >= 5 && (TALENT:淫乱 == 0 && !IS_習得早い(TARGET) && !IS_汚臭鈍感(TARGET) && TALENT:汚れ無視 == 0 && TALENT:精愛味覚 == 0)
	PRINTW これ以上UPするには素質が必要です
	RETURN 0
ELSEIF ABL:精液中毒 >= 10
	PRINTW 既にMAXです
	RETURN 0
;精液中毒＋レズ中毒は11以上にならない
;そんなファンタジー。例外多々あり、ABLUP33のコメント参照
ELSEIF ABL:精液中毒 + ABL:レズ中毒 + ABL:獣姦中毒 >= 10 && TALENT:精愛味覚 == 0 && TALENT:倒錯的 == 0 && TALENT:両刀 == 0 && TALENT:男性不信 == 0 && !IS_動物好き(TARGET) && TALENT:牝犬 == 0
	PRINTFORMW 精液中毒({ABL:精液中毒})＋レズ中毒({ABL:レズ中毒})＋獣姦中毒({ABL:獣姦中毒})の上限は10です
	RETURN 0
ENDIF

;必要な欲情の珠
A = 0
;必要な屈服の珠
B = 0
;必要な精液経験
C = 0
;必要な異常経験回数
D = 0

;条件別にＯＫかダメかを記録する
;精液経験多いバージョンの可否（I=0:可、I&1:珠不足、I&2:経験不足、I&4:能力不足）
I = 0
;精液経験少ないバージョンの可否（J=0:可、J&1:珠不足、J&2:経験不足、J&4:能力不足）
J = 0

CALL CAN_ABLUP32

;ＬＶ２から３、ＬＶ３から４、４から５に上げるときは異常経験必要（素質：[汚臭鈍感][中毒しやすい]なら無視できる）
SIF D > 0
	PRINTFORML %EXPNAME:[[EXP:異常経験]]%{D}以上(現在{EXP:異常経験})かつ

;奉仕精神が精液中毒＋１レベルでないといけない([淫乱]がない場合)
IF TALENT:淫乱 == 0
	PRINTFORML %ABLNAME:[[ABL:奉仕精神]]%{ABL:精液中毒 + 1}LV以上(現在{ABL:奉仕精神}LV)かつ
ELSEIF TALENT:淫乱 == 1
;欲望が精液中毒＋１レベルでないといけない([淫乱]がある場合)
	PRINTFORML %ABLNAME:[[ABL:欲望]]%{ABL:精液中毒 + 1}LV以上(現在{ABL:欲望}LV)かつ
ENDIF

;精液経験多いバージョン
PRINTFORM [0] - %PALAMNAME:[[PALAM:欲情]]%の珠×{JUEL:欲情}/{A} ……
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT 珠不足 
	SIF I & 2
		PRINT 経験不足 
	SIF I & 4
		PRINT 能力不足 
ENDIF
PRINTL 
PRINTFORML 　　　%PALAMNAME:[[PALAM:屈服]]%の珠×{JUEL:屈服}/{B}
PRINTFORML 　　　%EXPNAME:[[EXP:精液経験]]%　{EXP:精液経験}/{C}

;精液経験少ないバージョン
PRINTFORM [1] - %PALAMNAME:[[PALAM:欲情]]%の珠×{JUEL:欲情}/{A*3} ……
IF J == 0
	PRINT ＯＫ
ELSE
	SIF J & 1
		PRINT 珠不足 
	SIF J & 2
		PRINT 経験不足 
	SIF J & 4
		PRINT 能力不足 
ENDIF
PRINTL 
PRINTFORML 　　　%PALAMNAME:[[PALAM:屈服]]%の珠×{JUEL:屈服}/{B*3}
PRINTFORML 　　　%EXPNAME:[[EXP:精液経験]]%　{EXP:精液経験}/{C/2}

PRINTL [100] - やめる

INPUT
IF (RESULT < 0 || RESULT > 1) && RESULT != 100
	RESTART
ELSEIF I != 0 && RESULT == 0
	PRINTL 条件を満たしていません
	RESTART
ELSEIF J != 0 && RESULT == 1
	PRINTL 条件を満たしていません
	RESTART
ELSEIF RESULT == 100
	RETURN 0
ENDIF

ABL:精液中毒 += 1

IF RESULT == 0
	JUEL:欲情 -= A
	JUEL:屈服 -= B
ELSEIF RESULT == 1
	JUEL:欲情 -= A*3
	JUEL:屈服 -= B*3
ENDIF

PRINTFORML %ABLNAME:[[ABL:精液中毒]]%のレベルが{ABL:精液中毒}になりました

RETURN 0

;-------------------------------------------------
;精液中毒のLvUP可否判定
;-------------------------------------------------
@CAN_ABLUP32
;精液中毒はLv5が上限
;[淫乱][習得早い][汚臭鈍感][汚れ無視][精愛味覚]が付いている場合はLv10まで解放
SIF ABL:精液中毒 >= 10
	RETURN 0
SIF ABL:精液中毒 >= 5 && (TALENT:淫乱 == 0 && !IS_習得早い(TARGET) && !IS_汚臭鈍感(TARGET) && TALENT:汚れ無視 == 0 && TALENT:精愛味覚 == 0)
	RETURN 0
;精液中毒＋レズ中毒＋獣姦中毒は11以上にならない
SIF ABL:精液中毒 + ABL:レズ中毒 + ABL:獣姦中毒 >= 10 && TALENT:精愛味覚 == 0 && TALENT:倒錯的 == 0 && TALENT:両刀 == 0 && TALENT:男性不信 == 0 && !IS_動物好き(TARGET) && TALENT:牝犬 == 0
	RETURN 0

;判定変数を空に
I = 0
J = 0

A = 0
B = 0
C = 0
D = 0

IF ABL:精液中毒 == 0
	A = 3000
	B = 10000
	C = 10
ELSEIF ABL:精液中毒 == 1
	A = 8000
	B = 20000
	C = 25
ELSEIF ABL:精液中毒 == 2
	A = 15000
	B = 35000
	C = 40
ELSEIF ABL:精液中毒 == 3
	A = 30000
	B = 60000
	C = 80
ELSEIF ABL:精液中毒 == 4
	A = 50000
	B = 130000
	C = 200
ELSEIF ABL:精液中毒 == 5
	A = 65000
	B = 190000
	C = 500
ELSEIF ABL:精液中毒 == 6
	A = 90000
	B = 300000
	C = 800
ELSEIF ABL:精液中毒 == 7
	A = 120000
	B = 500000
	C = 1200
ELSEIF ABL:精液中毒 == 8
	A = 200000
	B = 800000
	C = 1500
ELSEIF ABL:精液中毒 == 9
	A = 500000
	B = 1500000
	C = 2000
ENDIF

;一線越えない
IF TALENT:一線越えない
	IF ABL:精液中毒 == 3
		TIMES A , 1.50
		TIMES B , 1.50
		TIMES C , 1.50
	ELSEIF ABL:精液中毒 == 4
		TIMES A , 2.00
		TIMES B , 2.00
		TIMES C , 2.00
	ELSEIF ABL:精液中毒 == 5
		TIMES A , 2.50
		TIMES B , 2.50
		TIMES C , 2.50
	ELSEIF ABL:精液中毒 >= 6
		TIMES A , 3.00
		TIMES B , 3.00
		TIMES C , 3.00
	ENDIF
ENDIF

;ＬＶ２以上に上げるときは異常経験必要（素質：[汚臭鈍感][中毒しやすい][倒錯的][狂気][精愛味覚]なら無視できる）
SIF ABL:精液中毒 >= 2 && (!IS_汚臭鈍感(TARGET) && TALENT:中毒しやすい == 0 && TALENT:倒錯的 == 0 && TALENT:狂気 == 0 && TALENT:精愛味覚 == 0)
	D = ABL:精液中毒 - 1

;反抗的
IF TALENT:ナマイキ
	TIMES A , 1.50
	TIMES B , 1.50
	TIMES C , 1.50
ENDIF
;感情乏しい
IF TALENT:感情乏しい
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF
;保守的
IF TALENT:保守的
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
ENDIF

;抑圧
IF IS_抑圧(TARGET)
	TIMES A , 1.20
	TIMES B , 1.20
	TIMES C , 1.20
;解放
ELSEIF IS_解放(TARGET)
	TIMES A , 0.80
	TIMES B , 0.80
	TIMES C , 0.80
ENDIF

;抵抗
IF TALENT:抵抗
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
ENDIF

;精愛味覚
IF TALENT:精愛味覚
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF

;舌使い
IF TALENT:舌使い
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF

;汚臭鈍感
IF IS_汚臭鈍感(TARGET)
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
;汚臭敏感
ELSEIF IS_汚臭敏感(TARGET)
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
ENDIF
;汚れ無視
IF TALENT:汚れ無視
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF

;中毒しやすい
IF TALENT:中毒しやすい
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF
;即落ち
IF TALENT:即落ち
	TIMES A , 0.50
	TIMES B , 0.50
	TIMES C , 0.50
ENDIF
;淫乱
IF TALENT:淫乱
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF
;色情狂
IF TALENT:絶倫
	TIMES A , 0.40
	TIMES B , 0.40
	TIMES C , 0.40
ENDIF
;倒錯的
IF TALENT:倒錯的
	TIMES A , 0.75
	TIMES B , 0.75
	TIMES C , 0.75
ENDIF
;小悪魔
IF TALENT:小悪魔
	TIMES A , 0.95
	TIMES B , 0.95
	TIMES C , 0.95
ENDIF
;狂気
IF TALENT:狂気
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF
;崩壊
IF TALENT:崩壊
	TIMES A , 0.90
	TIMES B , 0.90
	TIMES C , 0.90
ENDIF

IF TALENT:淫魔
	TIMES A , 0.01
	TIMES B , 0.01
	TIMES C , 0.01
ENDIF

;最低でも1個か1回は必要
SIF A < 1
	A = 1
SIF B < 1
	B = 1
SIF C < 1
	C = 1

IF D > EXP:異常経験
	;異常経験が不足
	I |= 2
	J |= 2
ENDIF

;奉仕精神が精液中毒＋１レベルでないといけない([淫乱]がない場合)
IF TALENT:淫乱 == 0
	IF ABL:奉仕精神 < ABL:精液中毒 + 1
		I |= 4
		J |= 4
	ENDIF
ELSEIF TALENT:淫乱 == 1
;欲望が精液中毒＋１レベルでないといけない([淫乱]がある場合)
	IF ABL:欲望 < ABL:精液中毒 + 1
		I |= 4
		J |= 4
	ENDIF
ENDIF

;精液経験多いバージョン
;欲情の珠が不足
SIF JUEL:欲情 < A
	I |= 1
;屈服の珠が不足
SIF JUEL:屈服 < B
	I |= 1
;精液経験が不足
SIF EXP:精液経験 < C
	I |= 2

;精液経験少ないバージョン
;欲情の珠が不足
SIF JUEL:欲情 < A*3
	J |= 1
;屈服の珠が不足
SIF JUEL:屈服 < B*3
	J |= 1
;精液経験が不足
SIF EXP:精液経験 < C/2
	J |= 2

IF I == 0 || J == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF
;
;
;